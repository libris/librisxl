apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'com.gradleup.shadow'

def mainClassName = "whelk.datatool.WhelkTool"

sourceSets {
    scripts {
        groovy { srcDir 'scripts' }
    }
    test {
        groovy { srcDir 'src/test/groovy/' }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':whelktool')
    implementation project(':whelk-core')
    scriptsCompileOnly sourceSets.main.output
    scriptsCompileOnly project(':whelk-core')
    testImplementation "org.spockframework:spock-core:${spockVersion}"
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveClassifier = null // removes `-all` in the filename of the created .jar
    project.setProperty("mainClassName", mainClassName)
}

jar {
    dependsOn ':server-common:jar'
    dependsOn(shadowJar)

    manifest {
        attributes "Main-Class": mainClassName,
                // log4j uses multi-release to ship different stack walking implementations for different java
                // versions. Since we repackage everything as a fat jar, that jar must also be multi-release.
                "Multi-Release": true
    }

    archiveClassifier = "nonfat"
    enabled = false
}