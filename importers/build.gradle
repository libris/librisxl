apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'war'
apply from: '../gretty.plugin'

loadConfiguration()
def loadConfiguration() {
    ext {
        try {
            environment = env
        } catch (MissingPropertyException mpe) {
            environment = "dev"
        }
    }
    def encoding = hasProperty('file.encoding') ? file.encoding : 'UTF-8'
    System.setProperty 'file.encoding', encoding
    System.setProperty 'whelk.environment', environment
}

mainClassName = "whelk.importer.ImporterMain"

def getGitVersion() {
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def versionNumber = getGitVersion()

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/main/java', 'src/main/groovy'] }
    }
    test {
        groovy { srcDir 'src/test/groovy/' }
    }
}

test {
    useJUnitPlatform()
}

test.testLogging {
    showStandardStreams true
    exceptionFormat "full"
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io/" }
}

// Don't blame me for this TRAVESTY. It is a necessity because of the versioning of xml-apis (2.0.2 which gradle otherwise chooses is OLDER (and broken) despite the version.)
configurations.all {
    resolutionStrategy {
        force "xml-apis:xml-apis:1.4.01"
    }
}

dependencies {
    // LIBRISXL modules
    implementation(project(':whelk-core'))

    // Common tools
    providedCompile 'javax.servlet:servlet-api:2.5'
    implementation "org.apache.groovy:groovy-all:${groovyVersion}"
    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.12'

    // Integration
    implementation 'mysql:mysql-connector-java:8.0.17'

    // metrics
    implementation "io.prometheus:simpleclient:${prometheusVersion}"

    // Logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"

    // profiling and test
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation 'cglib:cglib-nodep:3.1'
    implementation "org.apache.groovy:groovy-json:${groovyVersion}"
    implementation "org.apache.groovy:groovy-sql:${groovyVersion}"
    implementation "org.apache.groovy:groovy:${groovyVersion}"
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-simple:1.7.32'
}

// Include dependent libraries in archive.
jar {
    manifest {
        attributes "Main-Class": "whelk.importer.ImporterMain",
                // log4j uses multi-release to ship different stack walking implementations for different java
                // versions. Since we repackage everything as a fat jar, that jar must also be multi-release.
                "Multi-Release": true
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : project.zipTree(it).matching {
                exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA','build','.gradle/**','build.gradle','gradle','gradlew','gradlew.bat','test'
            }
        }
    }

}



configurations {
    executableWarDeps
}

project.afterEvaluate {
    war {
        archivesBaseName = "xlimporter"

        from {
            configurations.executableWarDeps.collect {
                //it.isDirectory() ? it : project.zipTree(it)
                it.isDirectory() ? it : project.zipTree(it).matching {
                }
            }
        }

        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        def manifestClasspath = configurations.runtimeClasspath.collect {
            "/WEB-INF/lib/${it.getName()}"
        }.join(',')

        manifest {
            attributes 'Implementation-Title': 'Standalone Whelk Importer',
                    'Implementation-Version': versionNumber,
                    'Class-Path': manifestClasspath
        }
    }
}

task(doRun, dependsOn: "classes", type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "whelk.importer.ImporterMain"
    systemProperties(
        'xl.secret.properties': System.getProperty("xl.secret.properties"),
        'xl.mysql.properties' : System.getProperty("xl.mysql.properties")
    )
    minHeapSize = "1g"
    maxHeapSize = "4g"
    args(System.getProperty("args", "").split() as String[])
}

gretty {
    httpPort = 8181
    contextPath = '/'
    systemProperties(
        'xl.secret.properties': System.getProperty("xl.secret.properties"),
        'xl.mysql.properties' : System.getProperty("xl.mysql.properties")
    )
}

task(groovy, dependsOn: "classes", type: JavaExec) {
    description "Run a Groovy Script (USAGE: -Dargs=\"<script> [...]\")"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "groovy.ui.GroovyMain"
    args(System.getProperty("args", "").split() as String[])
    systemProperties(
        'xl.secret.properties': System.getProperty("xl.secret.properties")
    )
}

task(console, dependsOn: 'classes', type: JavaExec) {
    description "Launch a Groovy Console"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'groovy.ui.Console'
}
