plugins {
    id "nebula.lint" version "20.2.0"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

allprojects {
    apply plugin: 'java'
    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    // Run manually with "./gradlew autoLintGradle"
    // Unused dependency warning cannot always be trusted, always verify ("this dependency is unused and can be removed")
    gradleLint.rules = ['all-dependency']
    gradleLint.alwaysRun = false


    configurations.all {
        // commons-logging and jcl-over-slf4j pulled in by different deps
        // resulting in duplicate org.apache.commons.logging.impl.* classes
        exclude group: "commons-logging", module: "commons-logging"

        // Not needed since Java 6
        exclude group: "stax", module: "stax-api"
    }

    apply plugin: 'codenarc'
    codenarc {
        configFile = new File("${project.projectDir}/../codenarc.config")
        ignoreFailures = true
        dependencies {
            codenarc("org.apache.groovy:groovy-all:${groovyVersion}")
            codenarc("org.codenarc:CodeNarc:${codenarcVersion}")
        }
    }
}