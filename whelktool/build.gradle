apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'application'


def loadConfiguration() {
    ext {
        try {
            environment = env
        } catch (MissingPropertyException mpe) {
            environment = "dev"
        }
    }
    def encoding = hasProperty('file.encoding') ? file.encoding : 'UTF-8'
    System.setProperty 'file.encoding', encoding
    System.setProperty 'whelk.environment', environment
}

loadConfiguration()

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/main/java', 'src/main/groovy'] }
    }
    test {
        groovy { srcDir 'src/test/groovy/' }
    }
}

mainClassName = "whelk.datatool.WhelkTool"

test.testLogging {
    showStandardStreams true
    exceptionFormat "full"
}

repositories {
    mavenCentral()
    maven { url "http://repository.codehaus.org/" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    compile project(':whelk-core')
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
    compile 'com.google.guava:guava:18.0'
    compile 'org.codehaus.groovy:groovy-cli-commons:2.5.4'
    compile 'org.codehaus.groovy:groovy-jsr223:2.5.4'
    compile 'org.codehaus.groovy:groovy:2.5.4'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.12'
}

jar {
    manifest {
        attributes "Main-Class": "whelk.datatool.WhelkTool"
    }

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : project.zipTree(it).matching {
            }
        }
    }
}

task(doRun, dependsOn: "classes", type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = "whelk.datatool.WhelkTool"
    minHeapSize = "1g"
    maxHeapSize = "4g"
    systemProperties(
        'xl.secret.properties': System.getProperty("xl.secret.properties")
    )
    args(System.getProperty("args", "").split() as String[])
}
