apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'com.gradleup.shadow'

def loadConfiguration() {
    ext {
        try {
            environment = env
        } catch (MissingPropertyException mpe) {
            environment = "dev"
        }
    }
    def encoding = hasProperty('file.encoding') ? file.encoding : 'UTF-8'
    System.setProperty 'file.encoding', encoding
    System.setProperty 'whelk.environment', environment
}

loadConfiguration()

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/main/java', 'src/main/groovy'] }
    }
    test {
        groovy { srcDir 'src/test/groovy/' }
    }
    // This is just so that intellij will understand whelk-core and whelktool classes 
    // inside whelktool scripts. So that autocomplete and navigation etc works.
    //
    // After importing the gradle project the 'scripts' directory needs to be manually 
    // unmarked as "source root" for whelktool classes to work. And so that 'scripts' 
    // is displayed as directories instead of as packages. Autocomplete etc. still works.
    //
    // Right-click on the 'scripts' directory and select 
    // "Mark directory as" > "Unmark as Sources root"
    whelktoolScripts {
        groovy { srcDir 'scripts' }
        groovy { srcDir 'src/main/resources/bulk-change-scripts' }
    }
}

test {
    useJUnitPlatform()
}

application {
    mainClass = "whelk.datatool.WhelkTool"
}

test.testLogging {
    showStandardStreams = true
    exceptionFormat = "full"
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io/" }
}

configurations.all {
    resolutionStrategy {
        force "xml-apis:xml-apis:1.4.01"
    }
}

dependencies {
    implementation project(':whelk-core')
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation 'junit:junit:4.13.2'
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.groovy:groovy-cli-commons:${groovyVersion}"
    implementation "org.apache.groovy:groovy-jsr223:${groovyVersion}"
    implementation "org.apache.groovy:groovy:${groovyVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation 'commons-codec:commons-codec:1.19.0'
    implementation group: 'xml-apis', name: 'xml-apis', version: '1.4.01'

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"

    whelktoolScriptsCompileOnly sourceSets.main.output
    whelktoolScriptsCompileOnly project(':whelk-core')
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    mergeServiceFiles()
    archiveClassifier = null // removes `-all` in the filename of the created .jar
}

jar {
    dependsOn ':server-common:jar'
    dependsOn(shadowJar)

    manifest {
        attributes "Main-Class": "whelk.datatool.WhelkTool",
                // log4j uses multi-release to ship different stack walking implementations for different java
                // versions. Since we repackage everything as a fat jar, that jar must also be multi-release.
                "Multi-Release": true
    }

    archiveClassifier = "nonfat"
    //enabled = false
}

task(doRun, dependsOn: "classes", type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "whelk.datatool.WhelkTool"
    minHeapSize = "1g"
    maxHeapSize = "4g"
    systemProperties(
        'xl.secret.properties': System.getProperty("xl.secret.properties")
    )
    args(System.getProperty("args", "").split() as String[])
}

task(groovy, dependsOn: "classes", type: JavaExec) {
    description "Run a Groovy Script (USAGE: -Dargs=\"<script> [...]\")"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = "groovy.ui.GroovyMain"
    args(System.getProperty("args", "").split() as String[])
}
