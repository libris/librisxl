apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'com.gradleup.shadow'

def mainClassName = "whelk.HouseKeepingServer"

repositories {
    mavenCentral()
}

// Don't blame me for this TRAVESTY. It is a necessity because of the versioning of xml-apis (2.0.2 which gradle otherwise chooses is OLDER (and broken) despite the version.)
configurations.all {
    resolutionStrategy {
        force "xml-apis:xml-apis:1.4.01"
    }
}

sourceSets {
    main {
        java { srcDirs = [] }
        groovy { srcDirs = ['src/main/java', 'src/main/groovy'] }
    }
    test {
        groovy { srcDir 'src/test/groovy/' }
    }
}


dependencies {
    // XL dependencies
    implementation(project(':whelk-core'))
    implementation(project(':server-common'))
    implementation(project(':whelktool'))

    // Groovy
    implementation "org.apache.groovy:groovy:${groovyVersion}"

    // Servlet
    api "jakarta.servlet:jakarta.servlet-api:${servletApiVersion}"
    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty.ee8:jetty-ee8-servlet:${jettyVersion}"

    // Logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4jVersion}"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4jVersion}"

    // metrics
    implementation "io.prometheus:simpleclient:${prometheusVersion}"
    implementation "io.prometheus:simpleclient_servlet:${prometheusVersion}"

    // Email
    implementation group: 'org.simplejavamail', name: 'simple-java-mail', version: '8.2.0'

    // Cron
    implementation group: 'it.sauronsoftware.cron4j', name: 'cron4j', version: '2.2.5'

    // Utils
    implementation "com.google.guava:guava:${guavaVersion}"

    // Testing
    testImplementation "org.spockframework:spock-core:${spockVersion}"
}

test {
    useJUnitPlatform()
}

test.testLogging {
    showStandardStreams = true
    exceptionFormat = "full"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    mergeServiceFiles()
    archiveClassifier = null // removes `-all` in the filename of the created .jar
}

jar {
    dependsOn ':server-common:jar'
    dependsOn(shadowJar)

    manifest {
        attributes "Main-Class": mainClassName,
                // log4j uses multi-release to ship different stack walking implementations for different java
                // versions. Since we repackage everything as a fat jar, that jar must also be multi-release.
                "Multi-Release": true
    }

    archiveClassifier = "nonfat"
    enabled = false
}

task(appRun, dependsOn: "classes", type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = mainClassName
    minHeapSize = "1g"
    maxHeapSize = "4g"
    systemProperties(
            'xl.secret.properties': System.getProperty("xl.secret.properties"),
            'xl.logRoot': System.getProperty("xl.logRoot", "./logs"),
            'xl.http.port': System.getProperty("xl.http.port", "8181"),
            'xl.test.rewriteSparqlResultIds': System.getProperty("xl.test.rewriteSparqlResultIds", "false")
    )
    args(System.getProperty("args", "").split() as String[])

    debugOptions {
        enabled = true
        port = 5006
        server = true
        suspend = false
    }
}
